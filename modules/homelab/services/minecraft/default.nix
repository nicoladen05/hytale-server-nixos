{
  lib,
  pkgs,
  config,
  inputs,
  ...
}:

{
  imports = [
    inputs.nix-minecraft.nixosModules.minecraft-servers
  ];

  options = {
    homelab.services.minecraft-server = {
      enable = lib.mkEnableOption "enable the minecraft server";

      servers = lib.mkOption {
        type = lib.types.attrsOf (
          lib.types.submodule {
            options = {
              type = lib.mkOption {
                type = lib.types.enum [
                  "vanilla"
                  "fabric"
                  "forge"
                  "quilt"
                  "paper"
                ];
                default = "vanilla";
                example = "fabric";
                description = ''
                  The type of Minecraft server to run. 
                  - `vanilla`: The official Minecraft server.
                  - `fabric`: A lightweight modding toolchain.
                  - `forge`: A popular modding platform.
                  - `quilt`: A modern modding platform that is a continuation of Fabric.
                  - `paper`: A high-performance fork of Spigot.
                '';
              };

              version = lib.mkOption {
                type = lib.types.str;
                default = "";
                example = "1.21.7";
                description = ''
                  The version of the Minecraft server to run.
                  If not specified, the latest version will be used.
                '';
                apply =
                  v:
                  if v == "" then
                    ""
                  else
                    let
                      parts = builtins.splitVersion v;
                    in
                    if builtins.length parts < 3 then
                      throw "Version must be in the format X.Y.Z"
                    else
                      # Transform version string (e.g. "1.21.7") into format -X_XX_X (e.g. "-1_21_7")
                      "-" + builtins.concatStringsSep "_" parts;
              };

              ram = lib.mkOption {
                type = lib.types.str;
                default = "4G";
                example = "2G";
                description = ''
                  The amount of RAM to allocate to the Minecraft server.
                  This should be a string with a number followed by a unit (e.g. "2G" for 2 gigabytes).
                '';
              };

              packwiz = lib.mkOption {
                type = lib.types.submodule {
                  options = {
                    enable = lib.mkOption {
                      type = lib.types.bool;
                      default = false;
                      description = "Enable packwiz support for this server.";
                    };
                    url = lib.mkOption {
                      type = lib.types.str;
                      default = "";
                      description = "The URL to the packwiz modpack.";
                    };
                    packHash = lib.mkOption {
                      type = lib.types.str;
                      default = "";
                      description = "The expected hash of the modpack for verification.";
                    };
                  };
                };
                default = { };
                description = "Options for packwiz modpack support.";
              };
            };
          }
        );
      };
    };
  };

  config = lib.mkIf config.homelab.services.minecraft-server.enable {
    nixpkgs.overlays = [ inputs.nix-minecraft.overlay ];

    services.minecraft-servers = {
      enable = true;
      eula = true;
      openFirewall = true;

      servers = lib.mapAttrs (
        serverName: serverConfig:
        let
          # compute attribute name for package lookup, e.g. "fabric-1_21_7"
          attrName = "${serverConfig.type}${serverConfig.version}";
          package = builtins.getAttr attrName pkgs.minecraftServers;
          modpack =
            if (serverConfig.packwiz != null) && serverConfig.packwiz.enable then
              pkgs.fetchPackwizModpack {
                inherit (serverConfig.packwiz) url packHash;
              }
            else
              null;
        in
        {
          enable = true;
          jvmOpts = "-Xmx${serverConfig.ram} -Xms${serverConfig.ram}";
          package = package;

          # only add symlinks when packwiz is enabled
          symlinks = lib.optionalAttrs ((serverConfig.packwiz != null) && serverConfig.packwiz.enable) {
            mods = "${modpack}/mods";
          };
        }
      ) config.homelab.services.minecraft-server.servers;
    };
  };
}
